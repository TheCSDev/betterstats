architectury { common parent["enabled_platforms"].split(',') }

dependencies
{
	// We depend on Fabric Loader here to use the Fabric @Environment annotations,
	// which get remapped to the correct annotations on each platform.
	// Do NOT use other classes from Fabric Loader.
	modImplementation "net.fabricmc:fabric-loader:${parent["fabric.loader_version"]}"
	modImplementation "dev.architectury:architectury:${parent["architectury.version"]}"

	//compile-time jar files in jarjar
	modCompileOnlyApi(fileTree(dir: 'src/main/resources/META-INF/jarjar', include: '*.jar'))
}

processResources
{
	//exclude nested jar files
	exclude('META-INF/jarjar/*.jar')

	//include the gradle properties file but renamed as 'mod-id.properties'
	from("../gradle.properties") { rename { "${parent["mod.id"]}.properties" } }

	//include the license file as well. necessary for legal reasons
	from("../LICENSE.md")

	//expand root project properties to potential mixin files
	filesMatching(["*.json", "*.mcmeta"]) { expand(properties: parent.properties) }
}
